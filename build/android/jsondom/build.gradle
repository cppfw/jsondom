apply plugin: 'com.android.library'
apply plugin: 'com.ydq.android.gradle.native-aar.export' // must go after android gradle plugin
apply plugin: 'com.ydq.android.gradle.native-aar.import' // must go after android gradle plugin

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        externalNativeBuild {
            cmake {
                targets "jsondom"
            }
        }
    }

    nativeBundleExport {
        headerDir = "${project.projectDir}/../../src/"
        bundleStatic = true
        includeHeaderFilter.add("**/*.hpp")
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt" // Provides a relative path to your CMake build script.
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {}
    }
}

repositories {
    maven {
        url 'http://gagis.hopto.org/nexus/repository/android/'
        allowInsecureProtocol = true
    }
}
dependencies {
    implementation 'io.github.cppfw:utki:+'
    implementation 'io.github.cppfw:papki:+'
}

// copy and rename release AAR to unified name
task copy_aar(type: Copy) {
    from file("build/outputs/aar/")
    into file("../")
    include("*-static-release.aar")
    rename { String fileName ->
        fileName.replace("static-release.aar", "\$(version).aar.in")
    }
}

afterEvaluate {
    copy_aar.dependsOn(assembleRelease)
    copy_aar.dependsOn(bundleStaticLibRelease)
}
